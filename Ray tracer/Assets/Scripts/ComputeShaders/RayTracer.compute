#pragma kernel TraceRays // 0

#include "RendererResources.hlsl"

// Constants
const int SpheresNum;
const float ScatterProbability;

// View settings
const int2 Resolution;
const int MaxBounceCount;
const int RaysPerPixel;
const float viewSpaceWidth;
const float viewSpaceHeight;
const float defocusStrength;
const float focalPlaneFactor;

// Camera orientation
const float3 WorldSpaceCameraPos;
const float3 CameraRotation;

// Per frame
const int FrameRand; // "random" value from 0 to 999999, gets reset each frame
const int FrameCount; // Each frame: FrameCount++

RWStructuredBuffer<Material2> Materials;
RWStructuredBuffer<Sphere> Spheres;
RWStructuredBuffer<Box> Boxes;
RWStructuredBuffer<Tri2> Tris;

RWTexture2D<float4> Result;

float3 ApplyCameraTransform (float3 point_)
{
    float3x3 rotationX = float3x3(
        1, 0, 0,
        0, cos(CameraRotation.x), -sin(CameraRotation.x),
        0, sin(CameraRotation.x), cos(CameraRotation.x)
    );
    float3x3 rotationY = float3x3(
        cos(CameraRotation.y), 0, sin(CameraRotation.y),
        0, 1, 0,
        -sin(CameraRotation.y), 0, cos(CameraRotation.y)
    );
    float3x3 rotationZ = float3x3(
        cos(CameraRotation.z), -sin(CameraRotation.z), 0,
        sin(CameraRotation.z), cos(CameraRotation.z), 0,
        0, 0, 1
    );

    // Apply rotations in the order of Z, X, Y
    // Applying the rotation matrices in the order of Z, Y, X, results in issues when rotating around the x axis (don't know why)
    point_ = mul(rotationZ, point_);
    point_ = mul(rotationX, point_);
    point_ = mul(rotationY, point_);

    return point_;
}

void IterateSpheres(inout HitInfo hitInfo, Ray ray)
{
    for (int i = 0; i < SpheresNum; i++)
    {
        Sphere sphere = Spheres[i];
        // From the equation: sqrLength(rayOrigin + rayDir * dst) = radius^2
        // Solving for dst results in a quadratic equation with coefficients:
        float3 oc = ray.origin - sphere.pos;
        float a = dot(ray.dir, ray.dir);
        float b = 2.0 * dot(oc, ray.dir);
        float c = dot(oc, oc) - sqr(sphere.radius);
        float discriminant = b * b - 4.0 * a * c;

        // Validate ray-sphere hit
        if (discriminant > 0)
        {
            float dst = (-b - sqrt(discriminant)) / (2.0 * a);
            float3 hitPoint = ray.origin + dst * ray.dir;

            // Only render the object in front
            // Also only render ray hits in the positive ray direction
            if (dst < hitInfo.dst && 0 <= dst)
            {
                hitInfo.didHit = true;
                hitInfo.dst = dst;
                hitInfo.hitPoint = hitPoint;
                hitInfo.normal = normalize(hitInfo.hitPoint - sphere.pos); // tangent is always the same as sphere radius direction
                hitInfo.material2 = Materials[sphere.materialKey];
            }
        }
    }
}

void IterateTriObjects(inout HitInfo hitInfo, Ray ray)
{

}

void IterateBoxes(inout HitInfo hitInfo, Ray ray)
{

}

HitInfo GetRayCollision (Ray ray)
{
    HitInfo hitInfo;
    hitInfo.dst = FLT_MAX;

    // Comment out to skip rendering of a chosen render object type
    IterateSpheres(hitInfo, ray);
    IterateTriObjects(hitInfo, ray);
    IterateBoxes(hitInfo, ray);

    return hitInfo;
}

TraceInfo Trace (Ray ray, inout int state)
{
    TraceInfo traceInfo;
    traceInfo.rayColor = float3(1, 1, 1); // Default is (1, 1, 1)
    traceInfo.incomingLight = float3(0, 0, 0); // Default incoming light is (0, 0, 0)

    for (int bounceCount = 0; bounceCount < MaxBounceCount; bounceCount++)
    {
        HitInfo hitInfo = GetRayCollision(ray);
        Material2 material = hitInfo.material2;
        
        if (hitInfo.didHit)
        {
            // Calculate new ray origin
            ray.origin = hitInfo.hitPoint - hitInfo.normal * 0.001; // epsilon offset

            // Calculate new ray direction
            // isSpecularBounce == False -> ray.dir = scatterDir
            // isSpecularBounce == True -> ray.dir = norm(lerp(scatterDir, specularDir, material.smoothness))
            bool isSpecularBounce = ScatterProbability < randNormalized(state);
            // Cosine weighted randPointOnUnitSphere. This removes the need for any "light spread" calculations
            float3 scatterDir = normalize(hitInfo.normal + randPointOnUnitSphere(state));
            float3 specularDir = reflect(ray.dir, hitInfo.normal);
            ray.dir = normalize(lerp(scatterDir, specularDir, material.smoothness * isSpecularBounce));

            // Validate ray direction
            if (dot(hitInfo.normal, ray.dir) < 0)
            {
                ray.dir = -ray.dir;
            }

            // Calculate new traceInfo values
            float3 emittedLight = material.color * material.brightness;
            traceInfo.incomingLight += emittedLight * traceInfo.rayColor;
            traceInfo.rayColor *= lerp(material.color, material.specularColor, isSpecularBounce);
        }
        else
        {
            break;
        }
    }

    return traceInfo;
}

[numthreads(TN_RT,TN_RT,1)]
void TraceRays (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)Resolution.x || id.y >= (uint)Resolution.y) { return; }
    int state = id.y * Resolution.x + id.x + FrameRand;

    // Calculate focal plane world pos for the given pixel
    float focalPosX = (float(id.x) / Resolution.x - 0.5) * 2.0 * viewSpaceWidth;
    float focalPosY = (float(id.y) / Resolution.y - 0.5) * 2.0 * viewSpaceHeight;
    float3 focalPos = float3(focalPosX, focalPosY, 1) * focalPlaneFactor;
    float3 worldFocalPos = ApplyCameraTransform(focalPos) + WorldSpaceCameraPos;

    // Trace ray(s) through the scene
    TraceInfo combinedTraceInfo;
    for (int i = 0; i < RaysPerPixel; i++)
    {
        // Initial ray setup
        float3 jitter = ApplyCameraTransform(float3(randPointInCircle(state) * defocusStrength, 0));
        Ray ray;
        ray.origin = WorldSpaceCameraPos + jitter * focalPlaneFactor;
        ray.dir = normalize(worldFocalPos - ray.origin);

        // Trace ray
        TraceInfo traceInfo = Trace(ray, state);

        // Record traceInfo
        combinedTraceInfo.rayColor += traceInfo.rayColor;
        combinedTraceInfo.incomingLight += traceInfo.incomingLight;
    }
    combinedTraceInfo.rayColor /= float(RaysPerPixel);
    combinedTraceInfo.incomingLight /= float(RaysPerPixel);

    // Combine prev frame with current frame. Weight the contributions to result in an average over all frames.
    float3 col = combinedTraceInfo.incomingLight;
    float3 colPrev = float3(Result[id.xy].x, Result[id.xy].y, Result[id.xy].z);

    float weight = 1.0 / (FrameCount + 1);

    float3 accumulatedCol = saturate(colPrev * (1 - weight) + col * weight);

    Result[id.xy] = float4(accumulatedCol.x, accumulatedCol.y, accumulatedCol.z, 0);
    // Result[id.xy] = float4(col.x, col.y, col.z, 0);
}